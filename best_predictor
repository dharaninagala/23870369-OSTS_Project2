#Author: Dharani Kumari Nagali
#Student Id: 23870369

#!/usr/bin/env bash

file="$1"  

#Check if file is provided as input 
if [ "$#" -ne 1 ]; then
    echo "Usage: $0 <tsv data file>" > "/dev/stderr"
    exit 1
fi

#To check whether database file is empty or does not contain any data.
if [ ! -s "$file" ]; then
    echo "The named input file $file does not exist or has zero length" > "/dev/stderr"
    exit 1
fi

# using awk to check whether the file is tab seprated or not 
awk -F'\t' 'NR==1 {
    if (NF > 1) 
    {
        exit 0;  
    } else 
    {
        exit 1;  
    }
}' "$file"

if [ $? -ne 0 ]; 
then
    echo "The input file $1 is not a tsv file" > "/dev/stderr"
    exit 1;
fi



# Use awk to clean data, check conditions, calculate sums, count valid entries, and calculate correlations
awk -F'\t' '
BEGIN { OFS="\t" }
NR>1 {

    #To remove any non-numeric characters except for number,decimal point and minus
    gsub(/[^0-9.-]/, "", $4);  # GDP
    gsub(/[^0-9.-]/, "", $5);  # Population
    gsub(/[^0-9.-]/, "", $6);  # Homicide Rate
    gsub(/[^0-9.-]/, "", $7);  # Life Expectancy
    gsub(/[^0-9.-]/, "", $8);  # Cantril Ladder Score

    country = $1;
    cantril = $8;

    process_data(country, cantril, $4, "GDP");
    process_data(country, cantril, $5, "Population");
    process_data(country, cantril, $6, "Homicide Rate");
    process_data(country, cantril, $7, "Life Expectancy");
}
END {
    # Variables to track the most predictive variable
    max_corr = 0;
    max_type = "";
    max_original_corr = 0;
    
    output_mean_correlation("Homicide Rate");
    output_mean_correlation("GDP");
    output_mean_correlation("Population");
    output_mean_correlation("Life Expectancy");

     # Output the most predictive mean correlation
    if (max_type != "") {
        printf "\n";
        printf "Most predictive mean correlation with the Cantril ladder is %s (r = %.3f)\n", max_type, max_original_corr;
    }

}

#function process_data is used to store all the values required to calculate correlation between predictor and cantril
function process_data(country, cantril, value, type)
{
    if (cantril != "" && value != "") 
    {
        var_prefix = type "_";
        sum[country, var_prefix "cantril"] += cantril;
        sum[country, var_prefix "value"] += value;
        sum_product[country, var_prefix "product"] += cantril * value;
        sum_sq[country, var_prefix "cantril_sq"] += cantril * cantril;
        sum_sq[country, var_prefix "value_sq"] += value * value;
        count[country, type]++;
    }
    
}

#function output_mean_correlation is used to calculate mean correlation of all countries for each predictor
function output_mean_correlation(type) 
{
    total_corr = num_countries = 0;
    for (key in count) 
    {
        # Split the key into country and variable type
        split(key, parts, SUBSEP);  
        country = parts[1];
        variable_type = parts[2];
        if (variable_type == type && count[country, type] >= 3) 
        {
            var_prefix = type "_";
            r = calculate_correlation(sum[country, var_prefix "cantril"], sum[country, var_prefix "value"], sum_product[country, var_prefix "product"], sum_sq[country, var_prefix "cantril_sq"], sum_sq[country, var_prefix "value_sq"], count[country, type]);
            total_corr += r;
            num_countries++;
        }
    }
    if (num_countries > 0) 
    {
        mean_corr = total_corr / num_countries;
        if (mean_corr == 0)
        {
            printf "Correlation of %s with Cantril ladder has no linear relationship or is undefined or zero due to lack of data variance\n", type;
        }
        #mean correlation is formatted to three decimal places
        else
        {
            printf "Mean correlation of %s with Cantril ladder is %.3f\n", type, mean_corr;
        }
        abs_corr = abs(mean_corr);  
        if (abs_corr > max_corr) 
        {
            max_corr = abs_corr;
            max_type = type;
            max_original_corr = mean_corr;
        }
    } 
    else 
    {
        printf "Mean correlation of %s with Cantril ladder is not available due to insufficient or invalid data\n", type;
    }
}


# Function calculate_correlation is used to calculate correlation between pair of values
function calculate_correlation(x_sum, y_sum, xy_sum, x2_sum, y2_sum, n) 
{
    numerator = ((n * xy_sum)- (x_sum * y_sum));
    denominator = sqrt(((n * x2_sum) - (x_sum^2)) * ((n * y2_sum) - (y_sum^2)));
    if (denominator == 0) 
    {
        return 0; 
    }
    return numerator/denominator;
}


# Function abs is to calculate the absolute value of a number 
function abs(x) 
{
    if (x < 0)
        return -x;
    else
        return x;
}
' "$file"





